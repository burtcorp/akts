buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'org.jetbrains.dokka' version '0.9.17'
    id 'maven-publish'
}

group 'io.burt'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3+'
    api 'org.junit.jupiter:junit-jupiter-api:5.3+'
    api 'org.jetbrains.kotlin:kotlin-scripting-jvm-host:1.3+'
    testImplementation 'org.jetbrains.kotlin:kotlin-scripting-common:1.3+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3+'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            pom {
                name = 'Akts'
                description = 'A test specification framework'
                url = 'http://github.com/burtcorp/akts'
                licenses {
                    license {
                        name = 'BSD 3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        name = 'Gustav Munkby'
                        email = 'munkby@burtcorp.com'
                        organization = 'Burt'
                        organizationUrl = 'http://burtcorp.com/'
                    }
                }
                scm {
                    connection = 'scm:git:git://git@github.com/burtcorp/akts.git'
                    developerConnection = 'scm:git:ssh://git@github.com/burtcorp/akts.git'
                    url = 'http://github.com/burtcorp/akts/'
                }
            }
        }
    }
}
